/*
 * Copyright (c) 2021 elementary, Inc. (https://elementary.io)
 *
 * This program is free software; you can redistribute it and/or
 * modify it under the terms of the GNU General Public
 * License as published by the Free Software Foundation; either
 * version 3 of the License, or (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
 * General Public License for more details.
 *
 * You should have received a copy of the GNU General Public
 * License along with this program; if not, write to the
 * Free Software Foundation, Inc., 51 Franklin Street, Fifth Floor,
 * Boston, MA 02110-1301 USA
 *
 * Authored by: Marius Meisenzahl <mariusmeisenzahl@gmail.com>
 */

public class WarningView : AbstractUpgradeView {
    public signal void next_step ();

    public WarningView () {
        Object (
            cancellable: true
        );
    }

    construct {
        var image = new Gtk.Image.from_icon_name ("dialog-warning", Gtk.IconSize.DIALOG) {
            vexpand = true
        };

        var primary_label = new Gtk.Label (_("Make sure you’re ready to upgrade")) {
            halign = Gtk.Align.START,
            max_width_chars = 60,
            wrap = true,
            xalign = 0
        };
        primary_label.get_style_context ().add_class (Granite.STYLE_CLASS_H2_LABEL);

        var secondary_label = new Gtk.Label (
            "· " + _("Before you upgrade, we recommend that you back up your data") + "\n" +
            "· " + _("To prevent data loss close all open applications and documents")
        ) {
            max_width_chars = 60,
            wrap = true,
            xalign = 0,
            use_markup = true
        };

        var grid = new Gtk.Grid () {
            row_spacing = 12,
            valign = Gtk.Align.CENTER
        };
        grid.attach (primary_label, 0, 0, 1, 1);
        grid.attach (secondary_label, 0, 1, 1, 1);

        content_area.column_homogeneous = true;
        content_area.halign = Gtk.Align.CENTER;
        content_area.margin = 48;
        content_area.margin_start = content_area.margin_end = 12;
        content_area.valign = Gtk.Align.CENTER;
        content_area.attach (image, 0, 0, 1, 1);
        content_area.attach (grid, 1, 0, 1, 2);

        var next_button = new Gtk.Button.with_label (_("Start upgrade"));
        next_button.get_style_context ().add_class (Gtk.STYLE_CLASS_SUGGESTED_ACTION);

        next_button.clicked.connect (() => {
            next_step ();
        });

        action_area.add (next_button);

        show_all ();
    }
}
